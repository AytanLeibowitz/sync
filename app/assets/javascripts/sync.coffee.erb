$ = jQuery

@Sync = 
  
  ready: false
  readyQueue: []
  FAYE_HOST: "<%= Sync.server %>"
  PUSHER_API_KEY: "<%= Sync.api_key %>"
  CLIENT_ADAPTER: "<%= Sync.adapter %>"

  init: ->
    @adapter = new Sync[@CLIENT_ADAPTER]
    $ => 
      @ready = true
      @connect()
      @flushReadyQueue()
      @bindUnsubscribe()


  # Handle Turbolinks teardown, unsubscribe from all channels before transition
  bindUnsubscribe: ->
    document.addEventListener "page:fetch", => @adapter.unsubscribeAll()


  onConnectFailure: (error) -> #noop

  connect: -> @adapter.connect()

  onReady: (callback) ->
    if @isReady()
      callback()
    else
      @readyQueue.push callback


  flushReadyQueue: -> 
    @onReady(callback) for callback in @readyQueue
    @readyQueue = []


  isReady: -> @ready

  camelize: (str) ->
    str.replace /(?:^|[-_])(\w)/g, (match, camel) -> camel?.toUpperCase() ? ''

  
  viewClassFromPartialName: (name) -> Sync[@camelize(name)] ? Sync.View


class Sync.Faye

  subscriptions: []

  connect: ->
    @client = new window.Faye.Client(Sync.FAYE_HOST)


  subscribe: (channel, callback) -> 
    @subscriptions.push(@client.subscribe channel, callback)


  unsubscribeAll: ->
    subscription.cancel() for subscription in @subscriptions
    @subscriptions = []


class Sync.Pusher

  subscriptions: []

  connect: ->
    @client = new window.Pusher(Sync.PUSHER_API_KEY)


  subscribe: (channel, callback) -> 
    @subscriptions.push channel
    channel = @client.subscribe(channel)
    channel.bind 'sync', callback


  unsubscribeAll: ->
    @client.unsubscribe(subscription) for subscription in @subscriptions
    @subscriptions = []


class Sync.View
  
  removed: false

  constructor: (@$el, @name) ->

  beforeUpdate: (html, data) -> @update(html)

  afterUpdate: -> #noop
  
  beforeInsert: ($el, data) -> @insert($el)

  afterInsert: -> #noop

  beforeRemove: -> @remove()

  afterRemove: -> #noop

  isRemoved: -> @removed

  remove: ->
    @$el.remove()
    @$el = $()
    @removed = true
    @afterRemove()


  bind: -> #noop

  show: -> @$el.show()

  update: (html) -> 
    $new = $($.trim(html))
    @$el.replaceWith($new)
    @$el = $new
    @afterUpdate()
    @bind()


  insert: ($el) -> 
    @$el.replaceWith($el)
    @$el = $el
    @afterInsert()
    @bind()



class Sync.Partial

  constructor: (@name, @channelUpdate, @channelDestroy, @selectorStart, @selectorEnd) ->
    @$start = $("[data-sync-id='#{@selectorStart}']")
    @$end   = $("[data-sync-id='#{@selectorEnd}']")
    @$el    = @$start.nextUntil(@$end)
    @view   = new (Sync.viewClassFromPartialName(@name))(@$el, @name)
    @adapter = Sync.adapter


  subscribe: ->
    @adapter.subscribe @channelUpdate, (data) => @update(data.html)
    @adapter.subscribe @channelDestroy, => @remove()

  
  update: (html) -> @view.beforeUpdate(html, {})

  remove: -> 
    @view.beforeRemove()
    @destroy() if @view.isRemoved()


  insert: (html) -> @view.beforeInsert($($.trim(html)), {})

  destroy: ->
    @$start.remove()
    @$end.remove()
    @$el?.remove()
    delete @$start
    delete @$end
    delete @$el


class Sync.PartialCreator

  defaultDirection: 'append'

  constructor: (@name, @channel, @selector, @direction) ->
    @$el = $("[data-sync-id='#{@selector}']")
    @direction ?= defaultDirection
    @adapter = Sync.adapter


  subscribe: ->
    @adapter.subscribe @channel, (data) =>
      @insert data.html,
              data.channelUpdate,
              data.channelDestroy,
              data.selectorStart,
              data.selectorEnd


  insertPlaceholder: (html) ->
    switch @direction
      when "append"  then @$el.before(html)
      when "prepend" then @$el.after(html)



  insert: (html, channelUpdate, channelDestroy, selectorStart, selectorEnd) ->
    @insertPlaceholder """
      <script type='text/javascript' data-sync-id='#{selectorStart}'></script>
      <script type='text/javascript' data-sync-el-placeholder></script>
      <script type='text/javascript' data-sync-id='#{selectorEnd}'></script>
    """
    partial = new Sync.Partial(
      @name,
      channelUpdate,
      channelDestroy,
      selectorStart,
      selectorEnd
    )
    partial.subscribe()
    partial.insert(html)


Sync.init()
