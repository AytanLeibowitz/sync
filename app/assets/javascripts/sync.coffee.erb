@Sync = 
  
  ready: false
  readyQueue: []
  FAYE_HOST: "<%= Sync.config[:server] %>"

  init: ->    
    $ => 
      @ready = true
      @connect()
      @flushReadyQueue()


  connect: ->
    window.client = @client = new Faye.Client(@FAYE_HOST)

  
  onReady: (callback) ->
    if @isReady()
      callback()
    else
      @readyQueue.push callback


  flushReadyQueue: -> 
    @onReady(callback) for callback in @readyQueue
    @readyQueue = []


  isReady: -> @ready

  camelize: (str) ->
    str.replace /(?:^|[-_])(\w)/g, (match, camel) -> camel?.toUpperCase() ? ''

  
  viewClassFromPartialName: (name) -> Sync[@camelize(name)] ? Sync.View


class Sync.View
  
  constructor: (@$el, @name) ->

  beforeUpdate: (html, data) -> @update(html)

  afterUpdate: -> #noop
  
  afterInsert: -> @$el.show()

  beforeRemove: -> @remove()

  afterRemove: -> #noop

  remove: -> 
    @$el.remove()
    @afterRemove()


  bind: -> #noop

  update: (html) -> 
    @$el.html($(html).html())
    @afterUpdate()
    @bind()



class Sync.Partial

  constructor: (@name, @channelUpdate, @channelDestroy, @selectorStart, @selectorEnd) ->
    @$el = $("[data-sync-id='#{@selectorStart}']").nextUntil("[data-sync-id='#{@selectorEnd}']")
    @view = new (Sync.viewClassFromPartialName(@name))(@$el, @name)


  subscribe: ->
    client.subscribe @channelUpdate, (data) => @update(data.html)
    client.subscribe @channelDestroy, => @remove()

  
  update: (html) -> @view.beforeUpdate(html, {})

  remove: -> @view.beforeRemove()


class Sync.PartialCreator

  constructor: (@name, @channel, @selector) ->
    @$el = $("[data-sync-id='#{@selector}']")


  subscribe: ->
    client.subscribe @channel, (data) => 
      @insert data.html,
              data.channelUpdate,
              data.channelDestroy,
              data.selectorStart,
              data.selectorEnd


  insert: (html, channelUpdate, channelDestroy, selectorStart, selectorEnd) ->
    @$el.before """
      <script type='text/javascript' data-sync-id='#{selectorStart}'>
      </script>
    """
    @$el.before $(html).hide()
    @$el.before """
      <script type='text/javascript' data-sync-id='#{selectorEnd}'>
      </script>
    """
    partial = new Sync.Partial(
      @name,
      channelUpdate,
      channelDestroy,
      selectorStart,
      selectorEnd
    )
    partial.subscribe()
    partial.view.afterInsert()

Sync.init()
